cmake_path(SET QL_MVN_SWIG_JAVA_RESOURCES_NATIVE_DIR ${QL_MVN_SWIG_JAVA_RESOURCES_DIR}/native)

if (WIN32)
    if (CMAKE_SIZEOF_VOID_P EQUAL 4)
        message(FATAL_ERROR "Windows x86 (32-bit) is not supported; please use an amd64 (64-bit) toolchain/generator.")
    endif ()
    if (CMAKE_SYSTEM_PROCESSOR MATCHES "^(x86_64|AMD64|amd64)$")
        cmake_path(APPEND QL_MVN_SWIG_JAVA_RESOURCES_NATIVE_DIR windows/amd64)
    elseif (CMAKE_SYSTEM_PROCESSOR MATCHES "^(aarch64|ARM64|arm64)$")
        cmake_path(APPEND QL_MVN_SWIG_JAVA_RESOURCES_NATIVE_DIR windows/arm64)
    else ()
        message(FATAL_ERROR "Unsupported Windows architecture '${CMAKE_SYSTEM_PROCESSOR}'; only x86_64/AMD64/amd64 and aarch64/ARM64/arm64 are supported.")
    endif ()
elseif (CMAKE_SYSTEM_NAME STREQUAL "Linux")
    if (CMAKE_SYSTEM_PROCESSOR MATCHES "^(x86_64|amd64)$")
        cmake_path(APPEND QL_MVN_SWIG_JAVA_RESOURCES_NATIVE_DIR linux/amd64)
    elseif (CMAKE_SYSTEM_PROCESSOR MATCHES "^(aarch64|arm64)$")
        cmake_path(APPEND QL_MVN_SWIG_JAVA_RESOURCES_NATIVE_DIR linux/arm64)
    else ()
        message(FATAL_ERROR "Unsupported Linux architecture '${CMAKE_SYSTEM_PROCESSOR}'; only x86_64/amd64 and aarch64/arm64 are supported.")
    endif ()
elseif (APPLE)
    cmake_path(APPEND QL_MVN_SWIG_JAVA_RESOURCES_NATIVE_DIR macos)
else ()
    message(FATAL_ERROR "Unsupported platform '${CMAKE_SYSTEM_NAME}'...")
endif ()

file(MAKE_DIRECTORY ${QL_MVN_SWIG_JAVA_RESOURCES_NATIVE_DIR})
file(MAKE_DIRECTORY ${QL_MVN_SWIG_JAVA_PACKAGE_DIR})
